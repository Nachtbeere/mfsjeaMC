plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group = pluginGroup
version = pluginVersion

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.3.72'

    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT'
    compile group: 'io.github.ranolp', name: 'mfsjea.jvm', version: '1.0.1'

    compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filteringCharset = 'UTF-8'

    filter ReplaceTokens, tokens: [
            version: version
    ]
}

shadowJar {
    archiveClassifier = ''
    dependencies {
        include dependency('org.jetbrains.kotlin::')
        include dependency(':mfsjea.jvm')
        include dependency(':java-semver')
    }
    relocate 'kotlin', 'io.github.ranolp.mfsjeamc.kotlin'
}

artifacts {
    archives shadowJar
}

task prepareUpload(type: Copy, dependsOn: ['build']) {
    from "$buildDir/libs"
    into "$buildDir/libs"
    include "mfsjeaMC-${project.version}.jar"
    rename "mfsjeaMC-${project.version}.jar", 'mfsjeaMC.jar'
}
